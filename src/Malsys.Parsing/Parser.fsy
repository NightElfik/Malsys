// Copyright © 2012 Marek Fišer [malsys@marekfiser.cz]
// All rights reserved.

//------------------------------------------------------------------------------
// Header of parser file.
//------------------------------------------------------------------------------

%{

open System
open Microsoft.FSharp.Text.Parsing

open Logger
open Malsys
open Malsys.Ast



// This function is called by the generated parser code. Returning initiates error recovery.
// Saves last error position to thread-static message logger.
let parse_error_rich = Some (fun (ctxt: ParseErrorContext<_>) ->
   Logger.setErrPos (new PositionRange(ctxt.ParseState.ResultRange))  )


let getLastErrorPos = Logger.getLastErrPos


let logMsg msgType (parserState : IParseState) =
    logMessagePos msgType (new PositionRange(parserState.ResultRange)) Array.empty;
    ()

let logMsgPos msgType pos =
    logMessagePos msgType pos Array.empty;
    ()


let getPos (parserState : IParseState) =
    new PositionRange(parserState.ResultRange)

let getPosI (parserState : IParseState) index =
    new PositionRange(parserState.InputRange(index))

let getPosRange (parserState : IParseState) fromI toI =
    new PositionRange((fst (parserState.InputRange(fromI)), snd (parserState.InputRange(toI))))

let getPosFromTo (fromPos : PositionRange) (toPos : PositionRange) =
    new PositionRange(fromPos.BeginLine, fromPos.BeginColumn, toPos.EndLine, toPos.EndColumn, fromPos.SourceName)

let getPosBetween (parserState : IParseState) fromI toI =
    new PositionRange((snd (parserState.InputRange(fromI)), fst (parserState.InputRange(toI))))

let getStartPos (parserState : IParseState) =
    let startPos = fst parserState.ResultRange
    new PositionRange(startPos, startPos)

let getEndPos (parserState : IParseState) =
    let endPos = snd parserState.ResultRange
    new PositionRange(endPos, endPos)

let getInpuFile (parserState : IParseState) =
    (fst parserState.ResultRange).FileName


let optionToStatement parseState (option : Option<#ILsystemStatement>) =
    match option with
        | Some(value) -> value
        | None -> new EmptyStatement(getPos parseState) :> ILsystemStatement

type ImmutableListPos_LsystemSymbol = ImmutableListPos<LsystemSymbol>

%}


//------------------------------------------------------------------------------
// Tokens lists.
//------------------------------------------------------------------------------

%token <string> ID
%token <Keyword> ABSTRACT ALL AS COMPONENT CONFIGURATION CONNECT CONSIDER CONTAINER DEFAULT EXTENDS FUN INTERPRET LET
%token <Keyword> LSYSTEM NOTHING OR PROCESS RETURN REWRITE SET SYMBOLS THIS TO TYPEOF USE VIRTUAL WEIGHT WITH WHERE
%token EQUALS SEMI COMMA QMARK COLON
%token LPAREN RPAREN LBRACE RBRACE
%token <float * ConstantFormat> FLOAT
%token <string> OPERATOR LBRACK RBRACK DOT
%token EOF


//------------------------------------------------------------------------------
// Priorities of tokens.
//------------------------------------------------------------------------------

// ID . LPAREN -> ExprFunc (not ExprMember ExprParen)
%nonassoc ID
%nonassoc LPAREN


//------------------------------------------------------------------------------
// Start (enter) rules and types.
//------------------------------------------------------------------------------

%start ParseInput
%type <InputBlock> ParseInput

// helper start rules especially for unit testing of parser

%start ParseExpression
%type <Expression> ParseExpression

%start ParseSymbols
%type <ImmutableListPos_LsystemSymbol> ParseSymbols

%%


//------------------------------------------------------------------------------
// Start rules.
//------------------------------------------------------------------------------

ParseInput:
    | InputStatementsList EOF
      { let sourceName = getInpuFile parseState in
        let statements = new ImmutableListPos<IInputStatement>($1 :> ResizeArray<IInputStatement>, getPos parseState) in
        new InputBlock(sourceName, statements) }


ParseExpression:
    | ExpressionNonEmpty EOF
      { $1 }

ParseSymbols:
    | LsystemSymbolList EOF
      { $1 }


//------------------------------------------------------------------------------
// Input.
//------------------------------------------------------------------------------

InputStatementsList:
    |
      { new ResizeArray<IInputStatement>() }
    | InputStatementsList InputStatement
      { $1.Add($2); $1 }

InputStatement:
    | ConstDef
      { ($1 :> IStatement) :?> IInputStatement }
    | FunDef
      { ($1 :> IStatement) :?> IInputStatement }
    | LsystemDef
      { $1 :> IInputStatement }
    | ProcessConfig
      { $1 :> IInputStatement }
    | ProcessStatement
      { $1 :> IInputStatement }
    | SEMI
      { new EmptyStatement(getPos parseState) :> IInputStatement }


//------------------------------------------------------------------------------
// Constant definition.
//------------------------------------------------------------------------------

ConstDef:
    | LET Id EQUALS Expression SEMI
      { new ConstantDefinition($2, $4, getPos parseState) :> IStatement }
    | LET error SEMI
      { logMsgPos ParsserMessage.ConstDefInvalid (getPosBetween parseState 1 3);
        new EmptyStatement(getPos parseState) :> IStatement }

//------------------------------------------------------------------------------
// Function definition.
//------------------------------------------------------------------------------

FunDef:
    | FUN Id OptParamsParens FunBody
      { new FunctionDefinition($2, $3, $4, getPos parseState) :> IStatement }
    | FUN Id error FunBody
      { logMsgPos ParsserMessage.ExcpectedParams (getPosBetween parseState 2 4);
        new EmptyStatement(getPos parseState) :> IStatement }

FunBody:
    | LBRACE FunStatementsList RBRACE
      { new ImmutableListPos<IFunctionStatement>($2 :> ResizeArray<IFunctionStatement>, getPosI parseState 1, getPosI parseState 3, getPos parseState)  }
    | LBRACE error RBRACE
      { logMsgPos ParsserMessage.FunStatementsInvalid (getPosBetween parseState 1 3);
        new ImmutableListPos<IFunctionStatement>(getPosI parseState 1, getPosI parseState 3, getPos parseState) }

FunStatementsList:
    |
      { new ResizeArray<IFunctionStatement>() }
    | FunStatementsList FunStatement
      { $1.Add($2); $1 }

FunStatement:
    | ConstDef
      { $1 :?> IFunctionStatement }
    | RETURN Expression SEMI
      { ($2) :> IFunctionStatement }


//------------------------------------------------------------------------------
// L-system definition.
//------------------------------------------------------------------------------

LsystemDef:
    | OptAbstract LSYSTEM Id OptParams OptBaseLsystems LsysBody
      { new LsystemDefinition($3, $1, $4, $5, $6, getPos parseState) :> IInputStatement }
    | OptAbstract LSYSTEM error LsysBody
      { logMsgPos ParsserMessage.LsystemHeaderInvalid (getPosBetween parseState 2 4);
        new EmptyStatement(getPos parseState) :> IInputStatement }

OptAbstract:
    |
      { false }
    | ABSTRACT
      { true }

OptBaseLsystems:
    |
      { ImmutableListPos<BaseLsystem>.Empty }
    | EXTENDS
      { logMsgPos ParsserMessage.EmptyBaseLsysBlock (getPosI parseState 1);
        ImmutableListPos<BaseLsystem>.Empty }
    | EXTENDS BaseLsystemListNonEmpty
      { new ImmutableListPos<BaseLsystem>($2 :> ResizeArray<BaseLsystem>, getPos parseState) }

BaseLsystemListNonEmpty:
    | BaseLsystem
      { let mutable x = new ResizeArray<BaseLsystem>() in x.Add($1); x }
    | BaseLsystemListNonEmpty COMMA BaseLsystem
      { $1.Add($3); $1 }

BaseLsystem:
    | Id OptExpressionsListParen
      { new BaseLsystem($1, $2, getPos parseState) }

LsysBody:
    | LBRACE LsysStatementsList RBRACE
      { new ImmutableListPos<ILsystemStatement>($2 :> ResizeArray<ILsystemStatement>, getPosI parseState 1, getPosI parseState 3, getPos parseState)  }
    | LBRACE error RBRACE
      { logMsgPos ParsserMessage.LsystemStatementsInvalid (getPosBetween parseState 1 3);
        new ImmutableListPos<ILsystemStatement>(getPosI parseState 1, getPosI parseState 3, getPos parseState)  }

LsysStatementsList:
    |
      { new ResizeArray<ILsystemStatement>() }
    | LsysStatementsList LsysStatement
      { $1.Add($2); $1 }

LsysStatement:
    | ConstDef
      { $1 :?> ILsystemStatement }
    | FunDef
      { $1 :?> ILsystemStatement }
    | LsysBasicStatementNoDelim SEMI
      { $1 :> ILsystemStatement }
    | SEMI
      { new EmptyStatement(getPos parseState) :> ILsystemStatement }

LsysBasicStatementNoDelim:
    | ComponentValueAssignDefNoDelim
      { $1 :> ILsystemStatement }
    | ComponentSymbolsAssignDefNoDelim
      { $1 :> ILsystemStatement }
    | RewriteRuleNoDelim
      { $1 :> ILsystemStatement }
    | SymbolsInterpretDefNoDelim
      { $1 :> ILsystemStatement }

ComponentValueAssignDefNoDelim:
    | SET Id EQUALS Expression
      { new ConstantDefinition($2, $4, true, getPos parseState) :> ILsystemStatement }
    | SET error
      { logMsgPos ParsserMessage.CompValueAssignDefInvalid (getPos parseState);
        new EmptyStatement(getPos parseState) :> ILsystemStatement }

ComponentSymbolsAssignDefNoDelim:
    | SET SYMBOLS Id EQUALS LsystemSymbolList
      { new SymbolsConstDefinition($3, $5, getPos parseState) :> ILsystemStatement }
    | SET SYMBOLS error
      { logMsgPos ParsserMessage.CompSymbolsAssignDefInvalid (getPos parseState);
        new EmptyStatement(getPos parseState) :> ILsystemStatement }

SymbolsInterpretDefNoDelim:
    | INTERPRET LsystemSymbolNoParamsList OptParams AS Id OptExpressionsListParen
      { let symbolsImm = new ImmutableListPos<Identifier>($2 :> ResizeArray<Identifier>, getPosI parseState 2) in
        new SymbolsInterpretDef(symbolsImm, $3, $5, $6, getPos parseState) :> ILsystemStatement }
    | INTERPRET LsystemSymbolNoParamsList OptParams AS LSYSTEM Id OptExpressionsListParen OptWithId
      { let symbolsImm = new ImmutableListPos<Identifier>($2 :> ResizeArray<Identifier>, getPosI parseState 2) in
        new SymbolsInterpretDef(symbolsImm, $3, $6, $7, true, $8, getPos parseState) :> ILsystemStatement }
    | INTERPRET error
      { logMsgPos ParsserMessage.SymbolsInterpretationInvalid (getPos parseState);
        new EmptyStatement(getPos parseState) :> ILsystemStatement }

OptWithId:
    |
      { Identifier.Empty }
    | WITH Id
      { $2 }

//------------------------------------------------------------------------------
// L-system's rewrite rule.
//------------------------------------------------------------------------------

RewriteRuleNoDelim:
    | REWRITE OptRrCtxt LsystemSymbol OptRrCtxt OptRrLocalConsts OptRrCondition TO RrReplacList
      { new RewriteRule($3, $2, $4, $5, $6, $8, getPos parseState) :> ILsystemStatement }
    | REWRITE OptRrCtxt LsystemSymbol OptRrCtxt OptRrLocalConsts OptRrCondition TO error
      { logMsgPos ParsserMessage.RrReplacementInvalid (getPosBetween parseState 7 9);
        new RewriteRule($3, $2, $4, $5, $6, ImmutableListPos<RewriteRuleReplacement>.Empty, getPos parseState) :> ILsystemStatement }
    | REWRITE error
      { logMsgPos ParsserMessage.RewriteRuleInvalid (getPos parseState);
        new EmptyStatement(getPos parseState) :> ILsystemStatement }

OptRrCtxt:
    |
      { ImmutableListPos<LsystemSymbol>.Empty }
    | LBRACE LsystemSymbolList RBRACE
      { $2.AddSeparators(getPosI parseState 1, getPosI parseState 3, getPos parseState) }
    | LBRACE error RBRACE
      { logMsgPos ParsserMessage.SymbolsPatternInvalid (getPosBetween parseState 1 3);
        ImmutableListPos<LsystemSymbol>.Empty }

OptRrLocalConsts:
    |
      { ImmutableListPos<ConstantDefinition>.Empty }
    | WITH
      { logMsgPos ParsserMessage.EmptyRrConstsDefBlock (getPosI parseState 1);
        ImmutableListPos<ConstantDefinition>(getStartPos parseState) }
    | WITH RrLocalConstsListNonEmpty
      { new ImmutableListPos<ConstantDefinition>($2 :> ResizeArray<ConstantDefinition>, getPos parseState) }
    | WITH error
      { logMsg ParsserMessage.RrConstsDefInvalid parseState;
        ImmutableListPos<ConstantDefinition>(getStartPos parseState) }

OptRrCondition:
    |
      { Expression.Empty }
    | WHERE
      { logMsgPos ParsserMessage.EmptyRrConditionBlock (getPosI parseState 1);
        new Expression(getEndPos parseState) }
    | WHERE ExpressionNonEmpty
      { $2 }
    | WHERE error
      { logMsg ParsserMessage.RrConditionInvalid parseState;
        new Expression(getEndPos parseState) }

RrReplacList:
    |
      { logMsgPos ParsserMessage.EmptyRrReplacementBlock (getStartPos parseState);
        ImmutableListPos<RewriteRuleReplacement>.Empty }
    | RrReplacListNonEmpty
      { new ImmutableListPos<RewriteRuleReplacement>($1 :> ResizeArray<RewriteRuleReplacement>, getPos parseState) }

RrReplacListNonEmpty:
    | RrReplac
      { let mutable x = new ResizeArray<RewriteRuleReplacement>() in x.Add($1); x }
    | RrReplacListNonEmpty TO RrReplac
      { $1.Add($3); $1 }
    | RrReplacListNonEmpty OR TO RrReplac
      { $1.Add($4); $1 }

RrReplac:
    | RrReplacSymbols OptRrWeight
      { new RewriteRuleReplacement($1, $2, getPos parseState) }

RrReplacSymbols:
    | NOTHING
      { new ImmutableListPos<LsystemSymbol>(getPos parseState) }
    | LsystemSymbolListNonEmpty
      { new ImmutableListPos<LsystemSymbol>($1 :> ResizeArray<LsystemSymbol>, getPos parseState) }

OptRrWeight:
    |
      { Expression.Empty }
    | WEIGHT
      { logMsg ParsserMessage.EmptyRrProbabilityBlock parseState;
        new Expression(getEndPos parseState) }
    | WEIGHT ExpressionNonEmpty
      { $2 }
    | WEIGHT error
      { logMsg ParsserMessage.RrProbabilityInvalid parseState;
        new Expression(getEndPos parseState) }



RrExprConst:
    | Id EQUALS Expression
      { new ConstantDefinition($1, $3, getPos parseState) }

RrLocalConstsListNonEmpty:
    | RrExprConst
      { let mutable x = new ResizeArray<ConstantDefinition>() in x.Add($1); x }
    | RrLocalConstsListNonEmpty COMMA RrExprConst
      { $1.Add($3); $1 }


//------------------------------------------------------------------------------
// L-system's symbols (also called `modules` in some L-system literature).
//------------------------------------------------------------------------------

LsystemSymbol:
    | Symbol OptExpressionsListParen
      { new LsystemSymbol($1, $2, getPos parseState) }

OptExpressionsListParen:
    |
      { ImmutableListPos<Expression>.Empty }
    | LPAREN ExpressionsList RPAREN
      { ($2 :> ImmutableListPos<Expression>).AddSeparators(getPosI parseState 1, getPosI parseState 3) }

LsystemSymbolList:
    |
      { ImmutableListPos<LsystemSymbol>.Empty }
    | LsystemSymbolListNonEmpty
      { new ImmutableListPos<LsystemSymbol>($1 :> ResizeArray<LsystemSymbol>, getPos parseState) }

LsystemSymbolListNonEmpty:
    | LsystemSymbol
      { let mutable x = new ResizeArray<LsystemSymbol>() in x.Add($1); x }
    | LsystemSymbolListNonEmpty LsystemSymbol
      { $1.Add($2); $1 }

LsystemSymbolNoParamsList:
    | Symbol
      { let sym = new Identifier($1, getPos parseState) in
        let mutable x = new ResizeArray<Identifier>() in x.Add(sym); x }
    | LsystemSymbolNoParamsList Symbol
      { $1.Add(new Identifier($2, getPosI parseState 1)); $1 }

Symbol:
    | ID
      { $1 }
    | OPERATOR
      { $1 }
    | LBRACK
      { $1 }
    | RBRACK
      { $1 }
    | DOT
      { $1 }


//------------------------------------------------------------------------------
// Parameters.
//------------------------------------------------------------------------------

OptParams:
    |
      { ImmutableListPos<OptionalParameter>.Empty }
    | OptParamsParens
      { $1 }

OptParamsParens:
    | LPAREN OptParamsList RPAREN
      { ($2 :> ImmutableListPos<OptionalParameter>).AddSeparators(getPosI parseState 1, getPosI parseState 3, getPos parseState) }
    | LPAREN error RPAREN
      { logMsgPos ParsserMessage.OptionalParamsInvalid (getPosBetween parseState 1 3);
        new ImmutableListPos<OptionalParameter>(getPosI parseState 1, getPosI parseState 3, getPos parseState) }

OptParamsList:
    |
      { ImmutableListPos<OptionalParameter>.Empty }
    | OptParamsListNonEmpty
      { new ImmutableListPos<OptionalParameter>($1 :> ResizeArray<OptionalParameter>, getPos parseState) }

OptParamsListNonEmpty:
    | OptParam
      { let mutable x = new ResizeArray<OptionalParameter>() in x.Add($1); x }
    | OptParamsListNonEmpty COMMA OptParam
      { $1.Add($3); $1 }

OptParam:
    | Id
      { new OptionalParameter($1, Expression.Empty, getPos parseState) }
    | Id EQUALS Expression
      { new OptionalParameter($1, $3, getPos parseState) }


//------------------------------------------------------------------------------
// Expressions.
//------------------------------------------------------------------------------

Expression:
    |
      { logMsg ParsserMessage.EmptyExpression parseState;
        new Expression(getStartPos parseState) }
    | ExpressionMembersNonEmpty
      { new Expression($1, getPos parseState) }

ExpressionNonEmpty:
    | ExpressionMembersNonEmpty
      { new Expression($1, getPos parseState) }

ExpressionMembersNonEmpty:
    | ExprMember
      { let mutable x = new ResizeArray<IExpressionMember>() in x.Add($1); x }
    | ExpressionMembersNonEmpty ExprMember
      { $1.Add($2); $1 }

ExprMember:
    | FLOAT
      { let (value, format) = $1 in
        new FloatConstant(value, format, getPos parseState) :> IExpressionMember }
    | Id
      { $1 :> IExpressionMember }
    | ExpressionsArray
      { $1 :> IExpressionMember }
    | OPERATOR
      { new Operator($1, getPos parseState) :> IExpressionMember }
    | ExprIndexer
      { $1 :> IExpressionMember }
    | ExprFun
      { $1 :> IExpressionMember }
    | ExprParen
      { $1 :> IExpressionMember }


ExpressionsArray:
    | LBRACE ExpressionsList RBRACE
      { new ExpressionsArray($2, getPosI parseState 1, getPosI parseState 3) }
    | LBRACE error RBRACE
      { logMsgPos ParsserMessage.ArrayExpressionInvalid (getPosBetween parseState 1 3);
        new ExpressionsArray(getPos parseState) }

ExpressionsList:
    |
      { new ImmutableListPos<Expression>(getPos parseState) }
    | ExpressionsListNonEmpty
      { new ImmutableListPos<Expression>($1 :> ResizeArray<Expression>, getPos parseState) }

ExpressionsListNonEmpty:
    | ExpressionNonEmpty
      { let mutable x = new ResizeArray<Expression>() in x.Add($1); x }
    | ExpressionsListNonEmpty COMMA Expression
      { $1.Add($3); $1 }

ExprIndexer:
    | LBRACK Expression RBRACK
      { new ExpressionIndexer($2, getPos parseState) }

// explicit ID to avoid shift/reduce conflict, don't know how to do just with `Id` (already tried `%prec ID` in many places)
ExprFun:
    | ID LPAREN ExpressionsList RPAREN
      { let id = new Identifier($1, getPos parseState) in
        let args = $3.AddSeparators(getPosI parseState 2, getPosI parseState 4)
        new ExpressionFunction(id, args, getPos parseState) :> IExpressionMember }

ExprParen:
    | LPAREN Expression RPAREN
      { new ExpressionBracketed($2, getPos parseState) }


//------------------------------------------------------------------------------
// Process configuration.
//------------------------------------------------------------------------------


ProcessConfig:
    | CONFIGURATION Id ProcessConfigBody
      { new ProcessConfigurationDefinition($2, $3, getPos parseState) }

ProcessConfigBody:
    | LBRACE ProcessConfigStatementsList RBRACE
      { new ImmutableListPos<IProcessConfigStatement>($2 :> ResizeArray<IProcessConfigStatement>, getPosI parseState 1, getPosI parseState 3, getPos parseState) }
    | LBRACE error RBRACE
      { logMsgPos ParsserMessage.ProcessConfigStatementsInvalid (getPosBetween parseState 1 3);
        new ImmutableListPos<IProcessConfigStatement>(getPosI parseState 1, getPosI parseState 3, getPos parseState)}

ProcessConfigStatementsList:
    |
      { new ResizeArray<IProcessConfigStatement>() }
    | ProcessConfigStatementsList ProcessConfigStatement
      { $1.Add($2); $1 }

ProcessConfigStatement:
    | ProcessComponentDef
      { $1 :> IProcessConfigStatement }
    | ProcessContainerDef
      { $1 :> IProcessConfigStatement }
    | ProcessConfigConnection
      { $1 :> IProcessConfigStatement }
    | SEMI
      { new EmptyStatement(getPos parseState) :> IProcessConfigStatement }


ProcessComponentDef:
    | COMPONENT Id TYPEOF FullyQualifiedTypeId SEMI
      { new ProcessComponent($2, $4, getPos parseState) }

ProcessContainerDef:
    | CONTAINER Id TYPEOF FullyQualifiedTypeId DEFAULT FullyQualifiedTypeId SEMI
      { new ProcessContainer($2, $4, $6, getPos parseState) :> IProcessConfigStatement }
    | CONTAINER Id TYPEOF FullyQualifiedTypeId SEMI
      { logMsgPos ParsserMessage.ProcessConfigContainerMissingDefaultType (getPosBetween parseState 4 5);
        new EmptyStatement(getPos parseState) :> IProcessConfigStatement }

ProcessConfigConnection:
    | OptVirtual CONNECT Id TO Id DOT Id SEMI
      { new ProcessConfigConnection($1, $3, $5, $7, getPos parseState) }

OptVirtual:
    |
      { false }
    | VIRTUAL
      { true }


FullyQualifiedTypeId:
    | ID
      { new Identifier($1, getPos parseState) }
    | FullyQualifiedTypeId DOT ID
      { new Identifier($1.Name + "." + $3, getPos parseState) }


//------------------------------------------------------------------------------
// Process statement.
//------------------------------------------------------------------------------

ProcessStatement:
    | PROCESS AllOrId OptExpressionsListParen WITH Id ProcessComponentAssignmentList ProcessComponentLsysStatementList SEMI
      { let processStats = new ImmutableListPos<ProcessComponentAssignment>($6 :> ResizeArray<ProcessComponentAssignment>, getPos parseState)
        let lsysStats = new ImmutableListPos<ILsystemStatement>($7 :> ResizeArray<ILsystemStatement>, getPos parseState)
        new ProcessStatement($2, $3, $5, processStats, lsysStats, getPos parseState) :> IInputStatement }

AllOrId:
    | ALL
      { Identifier.Empty }
    | Id
      { $1 }

ProcessComponentAssignmentList:
    |
      { let mutable x = new ResizeArray<ProcessComponentAssignment>() in x }
    | ProcessComponentAssignmentList ProcessComponentAssignment
      { $1.Add($2); $1 }

ProcessComponentAssignment:
    | USE FullyQualifiedTypeId AS Id
      { new ProcessComponentAssignment($2, $4, getPos parseState) }

ProcessComponentLsysStatementList:
    |
      { let mutable x = new ResizeArray<ILsystemStatement>() in x }
    | ProcessComponentLsysStatementList LsysBasicStatementNoDelim
      { $1.Add($2); $1 }


//------------------------------------------------------------------------------
// Other.
//------------------------------------------------------------------------------

Id:
    | ID
      { new Identifier($1, getPos parseState) }

IdsCommaList:
    |
      { new ImmutableListPos<Identifier>(getPos parseState) }
    | IdsCommaListNonEmpty
      { new ImmutableListPos<Identifier>($1 :> ResizeArray<Identifier>, getPos parseState) }

IdsCommaListNonEmpty:
    | Id
      { let mutable x = new ResizeArray<Identifier>() in x.Add($1); x }
    | IdsCommaListNonEmpty COMMA Id
      { $1.Add($3); $1 }