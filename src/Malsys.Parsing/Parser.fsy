%{

open System
open Microsoft.FSharp.Text.Parsing

open MessagesLogger
open Malsys
open Malsys.Ast
open Malsys.Compilers

let logError parserState msg =
    MessagesLogger.logMessage CompilerMessageType.Error parserState msg

let logErrorPos pos msg =
    MessagesLogger.logMessagePos CompilerMessageType.Error pos msg


// This function is called by the generated parser code. Returning initiates error recovery.
let parse_error_rich = Some (fun (ctxt: ParseErrorContext<_>) ->
    logError ctxt.ParseState "Parser error.")

let toPos (parserState : IParseState) =
    new Position(parserState.ResultRange)

%}

%token <string> ID
%token LSYSTEM LET FUN
%token EQUALS SEMI COMMA QMARK COLON RARROW
%token LPAREN RPAREN LBRACE RBRACE LBRACK RBRACK
%token <float> FLOAT
%token <string> OPERATOR
%token EOF


%start parseLsystemStatements
%type <IInputStatement[]> parseLsystemStatements

%start parseExpression
%type <Expression> parseExpression

%start parseExprInteractiveStatements
%type <IExprInteractiveStatement[]> parseExprInteractiveStatements

%%

//------------------------------------------------------------------------------
// Start rules
//------------------------------------------------------------------------------

parseLsystemStatements:
    | LsystemStatementsList EOF
      { ($1 :> ResizeArray<IInputStatement>).ToArray() }

parseExpression:
    | Expression EOF
      { $1 }

parseExprInteractiveStatements:
    | ExprInteractiveStatementList EOF
      { ($1 :> ResizeArray<IExprInteractiveStatement>).ToArray() }


//------------------------------------------------------------------------------
// Lsystem input
//------------------------------------------------------------------------------

LsystemStatementsList:
    |
      { new ResizeArray<IInputStatement>() }
    | LsystemStatementsList LsystemStatement
      { $1.Add($2); $1 }

LsystemStatement:
    | Lsystem
      { $1 :> IInputStatement }
    | MaybeVarDef SEMI
      { match $1 with
        | Some(varDef) -> varDef :> IInputStatement
        | None -> new EmptyStatement(toPos parseState) :> IInputStatement }
    | FunDef
      { $1 :> IInputStatement }


//------------------------------------------------------------------------------
// Expression interactive input
//------------------------------------------------------------------------------

ExprInteractiveStatementList:
    |
      { new ResizeArray<IExprInteractiveStatement>() }
    | ExprInteractiveStatementList ExprInteractiveStatement
      { $1.Add($2); $1 }
    | ExprInteractiveStatementList SEMI
      { $1 }

ExprInteractiveStatement:
    | Expression SEMI
      { $1 :> IExprInteractiveStatement }
    | MaybeVarDef SEMI
      { match $1 with
        | Some(varDef) -> varDef :> IExprInteractiveStatement
        | None -> new EmptyStatement(toPos parseState) :> IExprInteractiveStatement }
    | FunDef
      { $1 :> IExprInteractiveStatement }


//------------------------------------------------------------------------------
// Variable definition
//------------------------------------------------------------------------------

MaybeVarDef:
    | LetKw Id EQUALS Expression
      { Some (new VariableDefinition($1, $2, $4, toPos parseState)) }
    | LetKw EQUALS Expression
      { logErrorPos ($1.Position.EndPosition()) "Identifier excpected.";
        None }
    | LetKw error EQUALS Expression
      { logErrorPos ($1.Position.EndPosition()) "Identifier excpected.";
        None }

// Empty OR variable definitions divided by semicolon (but no semicolon on the end)
VarDefsList:
    |
      { new ResizeArray<VariableDefinition>() }
    | VarDefsListNonEmpty
      { $1 }

// Empty OR variable definitions divided by semicolon with extra semicolon on the end
VarDefsListSemi:
    |
      { new ResizeArray<VariableDefinition>() }
    | VarDefsListNonEmpty SEMI
      { $1 }

VarDefsListNonEmpty:
    | MaybeVarDef
      { let mutable x = new ResizeArray<VariableDefinition>() in
        match $1 with
        | Some(varDef) -> x.Add(varDef); x
        | None -> x }
    | VarDefsListNonEmpty SEMI MaybeVarDef
      { match $3 with
        | Some(varDef) -> $1.Add(varDef); $1
        | None -> $1 }


//------------------------------------------------------------------------------
// Function definition
//------------------------------------------------------------------------------

FunDef:
    | FunKw Id FunDefParams LBRACE RichExpression RBRACE
      { new FunctionDefinition($1, $2, $3, $5, toPos parseState) }

FunDefParams:
    | LPAREN OptParamsList RPAREN
      { new ImmutableListPos<OptionalParameter>($2, toPos parseState) }
    | LPAREN error RPAREN
      { logError parseState "Failed to parse parameters.";
        new ImmutableListPos<OptionalParameter>(toPos parseState) }

RichExpression:
    | VarDefsListSemi Expression
      { new RichExpression($1, $2, toPos parseState) }
    | error
      { logError parseState "Failed to parse 'rich expression', expression with optional local variables definition before it.";
        new RichExpression(Position.Unknown) }


OptParamsList:
    |
      { new ResizeArray<OptionalParameter>() }
    | OptParamsListNonEmpty
      { $1 }

OptParamsListNonEmpty:
    | OptParam
      { let mutable x = new ResizeArray<OptionalParameter>() in x.Add($1); x }
    | OptParamsListNonEmpty COMMA OptParam
      { $1.Add($3); $1 }

OptParam:
    | Id
      { new OptionalParameter($1, toPos parseState) }
    | Id EQUALS Expression
      { new OptionalParameter($1, $3, toPos parseState) }


//------------------------------------------------------------------------------
// L-system definition
//------------------------------------------------------------------------------

Lsystem:
    | LsysKw Id MaybeFunDefParams LBRACE LsysStatementsList RBRACE
      { new Lsystem($1, $2, $3, $5, toPos parseState) }

MaybeFunDefParams:
    |
      { new ImmutableListPos<OptionalParameter>(toPos parseState) }
    | FunDefParams
      { $1 }

LsysStatementsList:
    |
      { new ResizeArray<ILsystemStatement>() }
    | LsysStatementsList LsysStatement
      { $1.Add($2); $1 }

LsysStatement:
    | RewriteRule SEMI
      { $1 :> ILsystemStatement }
    | MaybeVarDef SEMI
      { match $1 with
        | Some(varDef) -> varDef :> ILsystemStatement
        | None -> new EmptyStatement(toPos parseState) :> ILsystemStatement }
    | FunDef
      { $1 :> ILsystemStatement }


//------------------------------------------------------------------------------
// L-system's rewrite rule
//------------------------------------------------------------------------------

RewriteRule:
    | MaybeLCtxt SymbolPattern MaybeRCtxt MaybeCondition MaybeProbab RARROW MaybeVarDefs SymbolWithArgsList
      { new RewriteRule($2, $1, $3, $4, $5, $7, $8, toPos parseState) }

MaybeLCtxt:
    |
      { new ImmutableListPos<SymbolPattern>(toPos parseState) }
    | LBRACE SymbolPatternsList RBRACE
      { new ImmutableListPos<SymbolPattern>($2, toPos parseState) }

MaybeRCtxt:
    |
      { new ImmutableListPos<SymbolPattern>(toPos parseState) }
    | LBRACE SymbolPatternsList RBRACE
      { new ImmutableListPos<SymbolPattern>($2, toPos parseState) }

MaybeCondition:
    |
      { new RichExpression(toPos parseState) }
    | QMARK LBRACE RichExpression RBRACE
      { new RichExpression($3, toPos parseState) }

MaybeProbab:
    |
      { new RichExpression(toPos parseState) }
    | COLON LBRACE RichExpression RBRACE
      { new RichExpression($3, toPos parseState) }

MaybeVarDefs:
    |
      { new ImmutableListPos<VariableDefinition>(toPos parseState) }
    | LBRACE VarDefsList RBRACE
      { new ImmutableListPos<VariableDefinition>($2, toPos parseState) }
    | LBRACE error RBRACE
      { logError parseState "Excpected variable definitions.";
        new ImmutableListPos<VariableDefinition>(toPos parseState) }


//------------------------------------------------------------------------------
// L-system's symbols (modules)
//------------------------------------------------------------------------------

SymbolPattern:
    | Symbol
      { new SymbolPattern($1, toPos parseState) }
    | Symbol LPAREN IdsCommaList RPAREN
      { new SymbolPattern($1, $3, toPos parseState) }

SymbolPatternsList:
    |
      { new ResizeArray<SymbolPattern>() }
    | SymbolPatternsList SymbolPattern
      { $1.Add($2); $1 }

SymbolWithArgs:
    | Symbol
      { new SymbolExprArgs($1, toPos parseState) }
    | Symbol LPAREN ExpressionsList RPAREN
      { new SymbolExprArgs($1, $3, toPos parseState) }

SymbolWithArgsList:
    |
      { new ResizeArray<SymbolExprArgs>() }
    | SymbolWithArgsList SymbolWithArgs
      { $1.Add($2); $1 }

Symbol:
    | ID
      { new Symbol($1, toPos parseState) }
    | OPERATOR
      { new Symbol($1, toPos parseState) }


//------------------------------------------------------------------------------
// Expressions
//------------------------------------------------------------------------------

Expression:
    |
      { logError parseState "Excpected non-empty expression.";
        new Expression(toPos parseState) }
    | ExpressionNonEmpty
      { new Expression($1, toPos parseState) }

ExpressionNonEmpty:
    | ExprMember
      { let mutable x = new ResizeArray<IExpressionMember>() in x.Add($1); x }
    | ExpressionNonEmpty ExprMember
      { $1.Add($2); $1 }

ExprMember:
    | Float
      { $1 :> IExpressionMember }
    | Id
      { $1 :> IExpressionMember }
    | ExpressionsArray
      { $1 :> IExpressionMember }
    | Operator
      { $1 :> IExpressionMember }
    | ExprIndexer
      { $1 :> IExpressionMember }
    | ExprFunc
      { $1 :> IExpressionMember }
    | ExprParen
      { $1 :> IExpressionMember }


ExpressionsArray:
    | LBRACE ExpressionsList RBRACE
      { new ExpressionsArray($2, toPos parseState) }
    | LBRACE error RBRACE
      { logError parseState "Expeted list of expression (divided by comma).";
        new ExpressionsArray(toPos parseState) }

ExpressionsList:
    |
      { new ResizeArray<Expression>() }
    | ExpressionsListNonEmpty
      { $1 }

ExpressionsListNonEmpty:
    | Expression
      { let mutable x = new ResizeArray<Expression>() in x.Add($1); x }
    | ExpressionsListNonEmpty COMMA Expression
      { $1.Add($3); $1 }

ExprIndexer:
    | LBRACK Expression RBRACK
      { new ExpressionIndexer($2, toPos parseState) }

ExprFunc:
    | Id LPAREN ExpressionsList RPAREN
      { new ExpressionFunction($1, $3, toPos parseState) }

ExprParen:
    | LPAREN Expression RPAREN
      { new ExpressionBracketed($2, toPos parseState) }


//------------------------------------------------------------------------------
// Primitives
//------------------------------------------------------------------------------

Id:
    | ID
      { new Identificator($1, toPos parseState) }

IdsCommaList:
    |
      { new ResizeArray<Identificator>() }
    | IdsCommaListNonEmpty
      { $1 }

IdsCommaListNonEmpty:
    | Id
      { let mutable x = new ResizeArray<Identificator>() in x.Add($1); x }
    | IdsCommaListNonEmpty COMMA Id
      { $1.Add($3); $1 }


Operator:
    | OPERATOR
      { new Operator($1, toPos parseState) }

Float:
    | FLOAT
      { new FloatConstant($1, toPos parseState) }


//------------------------------------------------------------------------------
// Keywords
//------------------------------------------------------------------------------

LsysKw:
    | LSYSTEM
      { new Keyword(toPos parseState) }

LetKw:
    | LET
      { new Keyword(toPos parseState) }

FunKw:
    | FUN
      { new Keyword(toPos parseState) }
