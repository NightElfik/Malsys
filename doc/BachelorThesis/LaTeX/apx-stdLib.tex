
\chapter{Standard library source code}
\label{chap:stdLib}

The Standard library was created for easier creation of inputs.
It contains useful constants, \lsystems for inheritance and predefined process configurations.
Standard library source code is prepended to all processed inputs in the web user interface.

Source code is divided into logical sections.
Each section contains short comment and explanation of source code.


\section{General Constants}

\begin{LsystemBreak}
let pi = 3.14159265358979323846;
let Ï€ = pi;
let e = 2.7182818284590452354;
\end{LsystemBreak}


\section{Component specific constants}

Constants defined in this section helps to add semantic meaning to numeric values which are used for configuring components.

\subsection{Svg renderer}

Following constants represent line cap values of \emph{LineCap} property of \hyperref[Malsys.Processing.Components.Renderers.SvgRenderer2D]{\emph{SvgRenderer2D}} component.

\begin{LsystemBreak}
let none = 0;
let square = 1;
let round = 2;
\end{LsystemBreak}


\subsection{ThreeJs renderer}

Following constants represent triangulation strategies which are set to \emph{PolygonTriangulationStrategy} property of \hyperref[Malsys.Processing.Components.Renderers.ThreeJsSceneRenderer3D]{\emph{ThreeJsSceneRenderer3D}} component.

\begin{LsystemBreak}
let fanFromFirstPoint = 0;
let minAngle = 1;
let maxAngle = 2;
let maxDistance = 3;
let maxDistanceFromNonTriangulated = 4;
\end{LsystemBreak}


\section{Abstract L-systems}

\lsystems defined in this section are intended to use as base \lsystems for \lsystems defined by user (for inheritance).
They are defined as \emph{abstract} to exclude them from processing with \emph{all} keyword.


\subsection{Standard \lsystem 2D}

\lsystem called \emph{StdLsystem} defines interpretation for usual symbols and correctly defines branches.

\begin{LsystemBreak}
abstract lsystem StdLsystem {
	interpret A B C D E F G as DrawForward(8);
	interpret a b c d e f g as MoveForward(8);

	interpret + as TurnLeft(90);
	interpret -(x = 90) as TurnLeft(-x);
	interpret | as TurnLeft(180);
	interpret / as Roll(180); // switches meaning of + and - symbols

	interpret < as StartPolygon;
	interpret . as RecordPolygonVertex;
	interpret > as EndPolygon;

	set symbols startBranchSymbols = [;
	set symbols endBranchSymbols = ];

	interpret [ as StartBranch;
	interpret ] as EndBranch;
}
\end{LsystemBreak}


\subsection{Standard \lsystem 3D}

\lsystem called \emph{StdLsystem3D} defines interpretation for usual symbols and correctly defines branches.
The only difference between \texttt{StdLsystem3D} and \texttt{StdLsystem} is in interpretation of + and - symbols.
2D image must be rendered in XY plane thus + and - symbols must do Pitch but in 3D for pitch is more intuitive to use \^{} and \& symbols.

\begin{LsystemBreak}
abstract lsystem StdLsystem3D {
	interpret A B C D E F G as DrawForward(8);
	interpret a b c d e f g as MoveForward(8);

	interpret + as Yaw(90);
	interpret -(x = 90) as Yaw(-x);
	interpret | as Yaw(180);

	interpret ^ as Pitch(90);
	interpret &(x = 90) as Pitch(-x);

	interpret / as Roll(90);
	interpret \(x = 90) as Roll(-x);

	interpret < as StartPolygon;
	interpret . as RecordPolygonVertex;
	interpret > as EndPolygon;

	set symbols startBranchSymbols = [;
	set symbols endBranchSymbols = ];

	interpret [ as StartBranch;
	interpret ] as EndBranch;
}
\end{LsystemBreak}


\subsection{Branches}

\lsystem \emph{Branches} defines interpretation for branches correctly.
To be able to do context rewriting correctly the Rewriter component must know what symbols start and end branches.
This should be the same symbol as supplied to interpreter.

\begin{LsystemBreak}
abstract lsystem Branches {
	set symbols startBranchSymbols = [;
	set symbols endBranchSymbols = ];

	interpret [ as StartBranch;
	interpret ] as EndBranch;
}
\end{LsystemBreak}


\subsection{Polygons and branches}

\lsystem \emph{Polygons} defines interpretation for polygons and branches.

\begin{LsystemBreak}
abstract lsystem Polygons {
	interpret < as StartPolygon;
	interpret . as RecordPolygonVertex;
	interpret > as EndPolygon;

	set symbols startBranchSymbols = [;
	set symbols endBranchSymbols = ];

	interpret [ as StartBranch;
	interpret ] as EndBranch;
}
\end{LsystemBreak}


\section{Process configurations}
\label{sec:stdLibProcessConfigurations}

\subsection{Symbol printer}

Process configuration \emph{SymbolPrinter} prints rewrited symbols as text.
It can be used to familiarize with \lsystem principles.
Advanced users can use is while "debugging" some more complex \lsystem.

\begin{LsystemBreak}
configuration SymbolPrinter {
	component AxiomProvider typeof AxiomProvider;
	component RandomGeneratorProvider
		typeof RandomGeneratorProvider;

	container Rewriter typeof IRewriter default SymbolRewriter;
	container Iterator typeof IIterator
		default MemoryBufferedIterator;
	container SymbolProcessor typeof ISymbolProcessor
		default SymbolsSaver;

	connect RandomGeneratorProvider
		to Iterator.RandomGeneratorProvider;
	connect AxiomProvider to Iterator.AxiomProvider;
	connect Iterator to Rewriter.SymbolProvider;
	connect Rewriter to Iterator.SymbolProvider;
	connect SymbolProcessor to Iterator.OutputProcessor;
}
\end{LsystemBreak}


\subsection{Svg renderer}
\label{configurationSvgRenderer}

Process configuration \emph{SvgRenderer} interprets symbols with \hyperref[Malsys.Processing.Components.Interpreters.TurtleInterpreter]{\emph{TurtleInterpreter}} component
	and renders the with \hyperref[Malsys.Processing.Components.Renderers.SvgRenderer2D]{\emph{SvgRenderer2D}} component to SVG (Scalable Vector Graphics).
However this process configuration is relatively universal and it can be used any 2D or 3D renderer component in the \emph{Renderer} container.
Note that \lsystem in interpreted in 3D but z-coordinate from data sent to \hyperref[Malsys.Processing.Components.Renderers.SvgRenderer2D]{\emph{SvgRenderer2D}} is cut off.

\begin{LsystemBreak}
configuration SvgRenderer {
	component AxiomProvider typeof AxiomProvider;
	component RandomGeneratorProvider
		typeof RandomGeneratorProvider;
	component LsystemInLsystemProcessor
		typeof LsystemInLsystemProcessor;

	container Rewriter typeof IRewriter default SymbolRewriter;
	container Iterator typeof IIterator
		default MemoryBufferedIterator;
	container InterpreterCaller typeof IInterpreterCaller
		default InterpreterCaller;
	container Interpreter typeof IInterpreter
		default TurtleInterpreter;
	container Renderer typeof IRenderer default SvgRenderer2D;

	connect RandomGeneratorProvider
		to Iterator.RandomGeneratorProvider;
	connect AxiomProvider to Iterator.AxiomProvider;
	connect Iterator to Rewriter.SymbolProvider;
	connect Rewriter to Iterator.SymbolProvider;
	connect InterpreterCaller to Iterator.OutputProcessor;
	connect LsystemInLsystemProcessor
		to InterpreterCaller.LsystemInLsystemProcessor;
	connect Renderer to Interpreter.Renderer;
}
\end{LsystemBreak}


\subsection{ThreeJs renderer}
\label{configurationThreeJsRenderer}

Process configuration \emph{ThreeJsRenderer} is very similar to \hyperref[configurationSvgRenderer]{\emph{SvgRenderer}}.
Only difference is in used renderer component in the \emph{Renderer} container.
This process configuration uses \hyperref[Malsys.Processing.Components.Renderers.ThreeJsSceneRenderer3D]{\emph{ThreeJsSceneRenderer3D}} to render 3D scene for Three.js engine [\ref{sec:ThreeJs}].

\begin{LsystemBreak}
configuration ThreeJsRenderer {
	component AxiomProvider typeof AxiomProvider;
	component RandomGeneratorProvider
		typeof RandomGeneratorProvider;
	component LsystemInLsystemProcessor
		typeof LsystemInLsystemProcessor;

	container Rewriter typeof IRewriter default SymbolRewriter;
	container Iterator typeof IIterator
		default MemoryBufferedIterator;
	container InterpreterCaller typeof IInterpreterCaller
		default InterpreterCaller;
	container Interpreter typeof IInterpreter
		default TurtleInterpreter;
	container Renderer typeof IRenderer
		default ThreeJsSceneRenderer3D;

	connect RandomGeneratorProvider
		to Iterator.RandomGeneratorProvider;
	connect AxiomProvider to Iterator.AxiomProvider;
	connect Iterator to Rewriter.SymbolProvider;
	connect Rewriter to Iterator.SymbolProvider;
	connect InterpreterCaller to Iterator.OutputProcessor;
	connect LsystemInLsystemProcessor
		to InterpreterCaller.LsystemInLsystemProcessor;
	connect Renderer to Interpreter.Renderer;
}
\end{LsystemBreak}


\subsection{Hexagonal ASCII renderer}

Process configuration \emph{HexAsciiRenderer} interpret symbols with special \hyperref[Malsys.Processing.Components.Interpreters.HexaAsciiInterpreter]{\emph{HexaAsciiInterpreter}}
	which can move forward by fixed steps (not by any distance) and it can turn only by multiples of sixty degrees thus result will be in hexagonal grid.
\hyperref[Malsys.Processing.Components.Interpreters.HexaAsciiInterpreter]{\emph{HexaAsciiInterpreter}} can communicate only with  is supposed to communicate with
	\hyperref[Malsys.Processing.Components.Renderers.TextRenderer]{\emph{TextRenderer}} component which is used to generate ASCII art-style output.

\begin{LsystemBreak}
configuration HexAsciiRenderer {
	component AxiomProvider typeof AxiomProvider;
	component RandomGeneratorProvider
		typeof RandomGeneratorProvider;
	component LsystemInLsystemProcessor
		typeof LsystemInLsystemProcessor;

	container Rewriter typeof IRewriter default SymbolRewriter;
	container Iterator typeof IIterator
		default MemoryBufferedIterator;
	container InterpreterCaller typeof IInterpreterCaller
		default InterpreterCaller;
	container Interpreter typeof IInterpreter
		default HexaAsciiInterpreter;
	container Renderer typeof IRenderer default TextRenderer;

	connect RandomGeneratorProvider
		to Iterator.RandomGeneratorProvider;
	connect AxiomProvider to Iterator.AxiomProvider;
	connect Iterator to Rewriter.SymbolProvider;
	connect Rewriter to Iterator.SymbolProvider;
	connect InterpreterCaller to Iterator.OutputProcessor;
	connect LsystemInLsystemProcessor
		to InterpreterCaller.LsystemInLsystemProcessor;
	connect Renderer to Interpreter.Renderer;
}
\end{LsystemBreak}


\subsection{Inner \lsystem process configuration}
\label{sec:innerLsystemConfig}

The \emph{InnerLsystemConfig} process configuration is used by the \hyperref[Malsys.Processing.Components.Common.ILsystemInLsystemProcessor]{\emph{ILsystemInLsystemProcessor}}
	component to interpret symbols as another \lsystem (\ref{sec:innerLsystem}).

Last connection is virtual because the \hyperref[Malsys.Processing.Components.Common.ILsystemInLsystemProcessor]{\emph{ILsystemInLsystemProcessor}} component will be artificially added
	to process configuration by itself (the \hyperref[Malsys.Processing.Components.Common.ILsystemInLsystemProcessor]{\emph{ILsystemInLsystemProcessor}} component).


\begin{LsystemBreak}
configuration InnerLsystemConfig {
	component Rewriter typeof SymbolRewriter;
	component Iterator typeof InnerLsystemIterator;
	component InterpreterCaller typeof InterpreterCaller;

	connect Iterator to Rewriter.SymbolProvider;
	connect Rewriter to Iterator.SymbolProvider;
	connect InterpreterCaller to Iterator.OutputProcessor;
	
	virtual connect LsystemInLsystemProcessor
		to InterpreterCaller.LsystemInLsystemProcessor;
}
\end{LsystemBreak}


\subsection{Constant dumper}

The \emph{ConstantDumper} process configuration contains single component, the \hyperref[Malsys.Processing.Components.Common.ConstantsDumper]{\emph{ConstantsDumper}} which just prints all defined global constants as text.
This can be used to experiment with expressions.

Even though \hyperref[Malsys.Processing.Components.Common.ConstantsDumper]{\emph{ConstantsDumper}} do not need any \lsystems to prints constants (\lsystems are actually ignored), process system of the library can process only \lsystems.
To over come this restriction with no effort the \emph{Constants} \lsystem is defined to be used in the process statement as follows:

\noindent
\texttt{process Constants with ConstantDumper}

\begin{LsystemBreak}
configuration ConstantDumper {
	component Dumper typeof ConstantsDumper;
}

abstract lsystem Constants { }
\end{LsystemBreak}



