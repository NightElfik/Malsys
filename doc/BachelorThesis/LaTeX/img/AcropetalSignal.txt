lsystem Generator extends Branches {

	set symbols axiom = [ + A(0) ] X;
	set iterations = 4;

	interpret F as MoveForward(90);
	interpret B as DrawSphere(8);
	interpret + as TurnLeft(90);

	interpret A(i) as lsystem AcropetalSignal(i);

	rewrite X to F [ + A(currentIteration) ] X;
}

abstract lsystem AcropetalSignal(i) extends Branches {

	set symbols axiom = B [ + A Y ] A [ - A Y ] A [ + A Y ] A Y;
	set symbols contextIgnore = + -;  // ignored in context search

	set iterations = i;

	interpret A as DrawForward(50, 2);
	interpret B as DrawForward(50, 4);
	interpret K as DrawSphere(8);
	interpret + as TurnLeft(45);
	interpret - as TurnLeft(-45);

	rewrite { B } A to B;
	rewrite { B } Y to K;

}

process all with SvgRenderer;